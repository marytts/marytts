apply plugin: 'application'

repositories {
    exclusiveContent {
        forRepository {
            maven {
                url 'https://raw.githubusercontent.com/DFKI-MLT/Maven-Repository/main'
            }
        }
        filter {
            includeGroup 'de.dfki.lt.jtok'
        }
    }
}

dependencies {
    runtimeOnly project(':marytts-runtime')
    runtimeOnly project(':marytts-languages').subprojects
    runtimeOnly project(':voice-cmu-slt-hsmm')
}

mainClassName = 'marytts.server.Mary'

run {
    dependsOn installDist

    // Define "mary.base"
    systemProperties << ['mary.base': installDist.destinationDir]

    // Forward server variables
    if ("socket.addr" in System.properties) {
        systemProperties << ['socket.addr': System.properties["socket.addr"]]
    }
    if ("socket.port" in System.properties) {
        systemProperties << ['socket.port': System.properties["socket.port"]]
    }

    // Forward logger variables
    if (logger.isEnabled(LogLevel.INFO)) {
        systemProperties << ['log4j.logger.marytts': 'INFO,stderr']
    }
    if (logger.isEnabled(LogLevel.DEBUG)) {
        systemProperties << ['log4j.logger.marytts': 'DEBUG,stderr']
    }

    // Override logger
    if ("log4j.logger.marytts" in System.properties) {
        systemProperties << ["log4j.logger.marytts": System.properties["log4j.logger.marytts"]]
    }


    doFirst {
        classpath += fileTree(installDist.destinationDir).include('lib/voice-*.jar', 'lib/marytts-lang-*.jar')
    }
}

task runInstallerGui(type: JavaExec) {
    group 'Application'
    description 'Runs the MaryTTS Installer GUI as a JVM application'
    dependsOn installDist
    classpath run.classpath
    mainClass = 'marytts.tools.install.InstallerGUI'
    systemProperties << ['mary.base': installDist.destinationDir]
}

distributions {
    main {
        contents {
            from 'download', {
                into 'download'
            }
            from 'LICENSE.md'
        }
    }
}

distTar {
    compression = Compression.BZIP2
    archiveExtension = 'tar.bz2'
}

startScripts {
    applicationName = 'marytts-server'
}

task installerGuiStartScripts(type: CreateStartScripts) {
    applicationName = 'marytts-component-installer'
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
    mainClass = 'marytts.tools.install.InstallerGUI'
    startScripts.finalizedBy it
}

tasks.named('distTar').configure {
    dependsOn installerGuiStartScripts
}

tasks.named('distZip').configure {
    dependsOn installerGuiStartScripts
}

tasks.withType(CreateStartScripts) {
    group 'Distribution'
    description startScripts.description
    doLast {
        // hack mary.base property into DEFAULT_JVM_OPTS
        unixScript.text = unixScript.text.replaceAll('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Dmary.base=\\$APP_HOME"')
        windowsScript.text = windowsScript.text.replaceAll('set DEFAULT_JVM_OPTS=', 'set DEFAULT_JVM_OPTS="-Dmary.base=%APP_HOME%"')
        // hack classpath
        unixScript.text = unixScript.text.replaceAll('-classpath "\\\\"\\$CLASSPATH\\\\""', '-classpath "\\\\"\\$APP_HOME/lib/*\\\\""')
        windowsScript.text = windowsScript.text.replaceAll('-classpath "%CLASSPATH%"', '-classpath "%APP_HOME%\\\\lib\\\\*"')
    }
}

installDist {
    preserve {
        include 'download/*', 'installed/*', 'lib/**'
    }
}

def testStartScriptsTask = tasks.register('testStartScripts') {
    dependsOn tasks.withType(CreateStartScripts)
    doLast {
        tasks.withType(CreateStartScripts).each { startScriptTask ->
            def unixScript = startScriptTask.unixScript
            def lastClasspathLine = unixScript.readLines().findAll { it.contains('-classpath') }.last()
            assert lastClasspathLine =~ '\\\$APP_HOME/lib/*': "$unixScript does not contain expected classpath"
        }
    }
}

tasks.named('check') {
    dependsOn testStartScriptsTask
}
