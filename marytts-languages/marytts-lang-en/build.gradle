plugins {
    id 'de.dfki.mary.component' version '0.1'
}
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/publishLogic.gradle"

marytts {
    component {
        name = 'English'
        packageName = 'marytts.language.en'
        configBaseClass = 'LanguageConfig'
        config from: 'component.yaml'
    }
}

repositories {
    ivy {
        url 'http://opennlp.sourceforge.net'
        patternLayout {
            artifact 'models-[revision]/[module].[ext]'
        }
    }
}

configurations {
    opennlp
}

dependencies {
    implementation project(':marytts-runtime')
    opennlp group: 'org.apache.opennlp', name: 'en-pos-maxent', version: '1.5', ext: 'bin'
    runtimeOnly group: 'de.dfki.mary', name: 'marytts-lexicon-en_US-cmudict', version: '0.1.0'
    testImplementation libs.xmlunit
    testImplementation project(path: ':marytts-runtime', configuration: 'testImplementation')
    integrationTestImplementation project(':marytts-runtime').sourceSets.test.output
}

processResources {
    from configurations.opennlp, {
        into "marytts/language/en/tagger"
    }
}

task generateComponentXmlDescriptor(type: GenerateComponentXmlDescriptor) {
    locales = ['en-US', 'en-GB']
}

rootProject.distributions.main.contents {
    from generateComponentXmlDescriptor, {
        into 'installed'
    }
}
