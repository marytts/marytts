plugins {
    id 'de.dfki.mary.component'
}
apply from: "$rootDir/buildLogic.gradle"
apply from: "$rootDir/publishLogic.gradle"

sourceCompatibility = 1.8

marytts {
    component {
        name = 'English'
        packageName = 'marytts.language.en'
        configBaseClass = 'LanguageConfig'
        config from: 'component.yaml'
    }
}

repositories {
    exclusiveContent {
        forRepository {
            ivy {
                url 'http://opennlp.sourceforge.net'
                allowInsecureProtocol = true
                patternLayout {
                    artifact 'models-[revision]/[module].[ext]'
                }
                metadataSources {
                    artifact()
                }
            }
        }
        filter {
            includeModule 'org.apache.opennlp', 'en-pos-maxent'
        }
    }
}

configurations {
    opennlp
}

dependencies {
    implementation project(':marytts-runtime')
    opennlp group: 'org.apache.opennlp', name: 'en-pos-maxent', version: '1.5', ext: 'bin'
    runtimeOnly group: 'de.dfki.mary', name: 'marytts-lexicon-en_US-cmudict', version: '0.1.0'
    testImplementation testLibs.xmlunit
    integrationTestImplementation project(':marytts-runtime').sourceSets.test.output
}

processResources {
    from configurations.opennlp, {
        into "marytts/language/en/tagger"
    }
}

task generateComponentXmlDescriptor(type: GenerateComponentXmlDescriptor) {
    locales = ['en-US', 'en-GB']
}

rootProject.distributions.main.contents {
    from generateComponentXmlDescriptor, {
        into 'installed'
    }
}
