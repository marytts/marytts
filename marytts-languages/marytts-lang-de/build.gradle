plugins {
    id 'de.dfki.mary.component'
}
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/buildLogic.gradle"
apply from: "$rootDir/publishLogic.gradle"

sourceCompatibility = 1.8

marytts {
    component {
        name = 'German'
        packageName = 'marytts.language.de'
        configBaseClass = 'LanguageConfig'
        config from: 'component.yaml'
    }
}

repositories {
    exclusiveContent {
        forRepository {
            ivy {
                url 'http://opennlp.sourceforge.net'
                allowInsecureProtocol = true
                patternLayout {
                    artifact 'models-[revision]/[module].[ext]'
                }
                metadataSources {
                    artifact()
                }
            }
        }
        filter {
            includeModule 'org.apache.opennlp', 'de-pos-maxent'
        }
    }
}

configurations {
    opennlp
}

dependencies {
    implementation project(':marytts-runtime')
    opennlp group: 'org.apache.opennlp', name: 'de-pos-maxent', version: '1.5', ext: 'bin'
    runtimeOnly group: 'de.dfki.mary', name: 'marytts-lexicon-de', version: '0.1.0'
    integrationTestImplementation project(':marytts-runtime').sourceSets.test.output
}

processResources {
    from configurations.opennlp, {
        into "marytts/language/de/tagger"
    }
}

task generateComponentXmlDescriptor(type: GenerateComponentXmlDescriptor)

rootProject.distributions.main.contents {
    from generateComponentXmlDescriptor, {
        into 'installed'
    }
}
